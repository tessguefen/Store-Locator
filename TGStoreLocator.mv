<MvFUNCTION NAME = "Module_Description" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvASSIGN NAME = "l.module:code"		VALUE = "TGStoreLocator">
	<MvASSIGN NAME = "l.module:name"		VALUE = "Super Fantastic Store Locator.">
	<MvASSIGN NAME = "l.module:provider"	VALUE = "Tess Guefen">
	<MvASSIGN NAME = "l.module:version"		VALUE = "1.000">
	<MvASSIGN NAME = "l.module:api_ver"		VALUE = "9.00">
	<MvASSIGN NAME = "l.module:description"	VALUE = "A super awesome description will eventually be here. STORE LOCATOR.">
	<MvASSIGN NAME = "l.module:features"	VALUE = "data_store, system, vis_system, json, clientside, component">
</MvFUNCTION>

<MvCOMMENT>
| ========================================================================================================================
|	INSTALL
| ========================================================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Install_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvCOMMENT>
		Create Settings Table
	</MvCOMMENT>

	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGSL_Settings
							(
								name	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 ) $	',
								value	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 ) $ '
							)' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGStoreLocator-INSTALL-0001', 'An error occured while creating the table ' $ g.Store_Table_Prefix $ 'TGSL_Settings. Please make sure this table was not already created.' ) }">
	</MvIF>

	<MvQUERY	NAME	= "Merchant"
				QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGSL_Settings_1 ON ' $ g.Store_Table_Prefix $ 'TGSL_Settings ( name )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGStoreLocator-INSTALL-0002', 'An error occured while creating the unique index.' ) }">
	</MvIF>

	<MvCOMMENT>
		Insert Default Settings
	</MvCOMMENT>
	<MvQUERY	NAME	= "Merchant"
				QUERY	= "{ 'INSERT INTO ' $ g.Store_Table_Prefix $ 'TGSL_Settings ( name, value ) VALUES
								( \'DefaultSearchRadius\', \'50\' ),
								( \'SearchRadiusOptions\', \'10,20,50,100\' ),
								( \'MapCenterLatLng\', \'39.8283,-98.5795\' ),
								( \'Geocode_Url\', \'https://maps.googleapis.com/maps/api/geocode/json\' ),
								( \'Geocode_Apikey\', \'\' ),
								( \'Maps_Apikey\', \'\' ),
								( \'Maps_Url\', \'https://maps.googleapis.com/maps/api/js\' )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGStoreLocator-INSTALL-0011', 'An error occured while adding the Default Settings.' ) }">
	</MvIF>

	<MvCOMMENT>
		Create Main Locations Table
	</MvCOMMENT>
	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGSL_Locations
							(
								id		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 255 )	$ ',
								code	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 )		$ ',
								name	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 )		$ ',
								addr1	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 )		$ ',
								addr2	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 )		$ ',
								city	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 )		$ ',
								state	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 )		$ ',
								zip		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 )		$ ',
								cntry	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 )		$ ',
								lat		FLOAT( 10, 6 ) NOT NULL,
								lng		FLOAT( 10, 6 ) NOT NULL,
								active	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_BOOL()	$ '
							)' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGStoreLocator-INSTALL-0003', 'An error occured while creating the table ' $ g.Store_Table_Prefix $ 'TGSL_Locations. Please make sure this table was not already created.' ) }">
	</MvIF>

	<MvQUERY	NAME	= "Merchant"
				QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGSL_Locations_1 ON ' $ g.Store_Table_Prefix $ 'TGSL_Locations ( id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGStoreLocator-INSTALL-0004', 'An error occured while creating the unique index.' ) }">
	</MvIF>

	<MvQUERY	NAME	= "Merchant"
				QUERY	= "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'TGSL_Locations_2 ON ' $ g.Store_Table_Prefix $ 'TGSL_Locations ( code )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGStoreLocator-INSTALL-0005', 'An error occured while creating the unique index.' ) }">
	</MvIF>

	<MvCOMMENT>
		Create Additional Fields table.
	</MvCOMMENT>

	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGSL_AddlFields
							(
								id			' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								code		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 )		$ ',
								name		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 )		$ '
							)
				' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGStoreLocator-INSTALL-0006', 'An error occured while creating the table ' $ g.Store_Table_Prefix $ 'TGSL_AddlFields. Please make sure this table was not already created.' ) }">
	</MvIF>

	<MvQUERY	NAME	= "Merchant"
				QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGSL_AddlFields_1 ON ' $ g.Store_Table_Prefix $ 'TGSL_AddlFields ( id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGStoreLocator-INSTALL-0007', 'An error occured while creating the unique index.' ) }">
	</MvIF>

	<MvQUERY	NAME	= "Merchant"
				QUERY	= "{ 'CREATE INDEX ' $ g.Store_Table_Prefix $ 'TGSL_AddlFields_2 ON ' $ g.Store_Table_Prefix $ 'TGSL_AddlFields ( code )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGStoreLocator-INSTALL-0008', 'An error occured while creating an index.' ) }">
	</MvIF>

	<MvCOMMENT>
		Create Additional Field Values table.
	</MvCOMMENT>

	<MvQUERY	NAME = "Merchant"
				QUERY = "{ 'CREATE TABLE ' $ g.Store_Table_Prefix $ 'TGSL_AddlValues
							(
								field_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								location_id	' $ [ g.Module_Library_Native_DBAPI ].DB_Type_NUMBER( 0, 0 )	$ ',
								value		' $ [ g.Module_Library_Native_DBAPI ].DB_Type_CHAR( 255 )		$ '
							)
				' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGStoreLocator-INSTALL-0009', 'An error occured while creating the table ' $ g.Store_Table_Prefix $ 'TGSL_AddlValues. Please make sure this table was not already created.' ) }">
	</MvIF>

	<MvQUERY NAME	= "Merchant"
			 QUERY	= "{ 'CREATE UNIQUE INDEX ' $ g.Store_Table_Prefix $ 'TGSL_AddlValues_1 ON ' $ g.Store_Table_Prefix $ 'TGSL_AddlValues ( field_id, location_id )' }">
	<MvIF EXPR = "{ g.MvQUERY_Error }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGStoreLocator-INSTALL-0010', 'An error occured while creating the unique index.' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGSL_Locations', 1 ) }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGStoreLocator-INSTALL-0011', 'An error occured while creating the store key, TGSL_Locations. Please make sure this does not already exsist.' ) }">
	</MvIF>

	<MvIF EXPR = "{ NOT [ g.Module_Library_DB ].StoreKey_Insert( 'TGSL_AddlFields', 1 ) }">
		<MvASSIGN NAME = "l.void" VALUE = "{ Module_Uninstall_Store( l.module ) }">
		<MvFUNCTIONRETURN VALUE = "{ [ g.Module_Library_Utilities ].Error( 'TGStoreLocator-INSTALL-0012', 'An error occured while creating the store key, TGSL_AddlFields. Please make sure this does not already exsist.' ) }">
	</MvIF>

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Upgrade_Store" PARAMETERS = "module var, version" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_Uninstall_Store" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "" ERROROUTPUTLEVEL = "">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'TGSL_Settings' }">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'TGSL_Locations'}">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'TGSL_AddlFields'}">
	<MvQUERY NAME = "Merchant" QUERY = "{ 'DROP TABLE ' $ g.Store_Table_Prefix $ 'TGSL_AddlValues'}">

	<MvASSIGN NAME = "l.success" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGSL_Locations' ) }">
	<MvASSIGN NAME = "l.success" VALUE = "{ [ g.Module_Library_DB ].StoreKey_Delete( 'TGSL_AddlFields' ) }">
	<MvFUNCTIONRETURN VALUE = 1 />
</MvFUNCTION>


<MvCOMMENT>
| ====================================================================================
|	SYSTEM
| ====================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "SystemModule_Action" PARAMETERS = "module var, action" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SystemModule_Screen" PARAMETERS = "module var, screen" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "SystemModule_UIException" PARAMETERS = "module var, exception" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>


<MvCOMMENT>
| ====================================================================================
|	VIS_SYSTEM
| ====================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "Module_System_Tabs" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = "TGSL_STORES:Store Locator">
</MvFUNCTION>

<MvFUNCTION NAME = "Module_System_Head" PARAMETERS = "module var, tab" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR= "{ ( l.tab NE 'TGSL_STORES' ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>
	<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_JavaScript() }">
	<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_CSS() }">
	<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_Modal_CSS() }">
	<style>
		#TGSL_Settings_Wrapper {font-family: 'MuseoSans', Arial, Helvetica, sans-serif;}
		#TGSL_Settings_Wrapper th, #TGSL_Settings_Wrapper td {padding: 8px;}
		.TGSL_Settings_dialog_field {padding-top: 4px;}
		#TGSL_Settings_Wrapper th {background: #f3f3f3;}
		.tgsl_error .mm_dialog_prompt_required {color: #ff6b59;}
		.tgsl_error input, .tgsl_error textarea, .tgsl_error select {border-color: #ff6b59;}
		.tgsl_settings_errors {background-color: #ff6b59;border-color: #ff6b59;color: #fff;padding: 5px 10px;font-weight: bold;margin: 10px 0;}
		.TGSL_Settings_dialog_field input, .TGSL_Settings_dialog_field textarea, .TGSL_Settings_dialog_field select {display: block;width: 100%;border: 1px solid #d9d9d9;padding: 5px 10px;box-sizing: border-box;}
		<MvCOMMENT>.tgsl_hr { border: 0;border-top: 1px solid #d9d9d9;padding-top: 1px;}</MvCOMMENT>
	</style>
	<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_Modal_JavaScript() }">
	<script language="JavaScript" src="{ g.clientside_url $ 'Module_Code=' $ encodeattribute( l.module:code ) $ '&Filename=StoreLocator.js' }"></script>
	<script language="JavaScript">MMScreen_LoadFinished( function() { new StoreLocator_Batchlist(); } );</script>
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_System_Content" PARAMETERS = "module var, tab, load_fields" STANDARDOUTPUTLEVEL = "text, html, compresswhitespace">
	<MvIF EXPR= "{ ( l.tab NE 'TGSL_STORES' ) }">
		<MvFUNCTIONRETURN VALUE = 1>
	</MvIF>

	<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_MMBatchList_HTML() }">

	<div id="jsStoreLocator_Batchlist"></div>
	<span id="TGStoreLocator_mmbatchlist_persistent_filters" class="mm9_batchlist_action_row_section" style="font-size:16px;">
		Store Locator
	</span>

	<!-- TGSL_Settings MM Dialog -->
	<div id="TGSL_Settings" class="mm_dialog">
		<MvASSIGN NAME = "l.void" VALUE = "{ Settings_Load_All( l.tgsl_settings ) }">
		<div id="TGSL_Settings_dialog_title" class="mm_dialog_title">Settings</div>
		<div class="mm_clear"></div>
		<div id="TGSL_Settings_Errors" class="tgsl_settings_errors" style="display: none;"></div>
		<table id="TGSL_Settings_Wrapper">
			<tbody>
				<tr>
					<th colspan="2" align="left">UI Settings</th>
				</tr>
				<tr>
					<td valign="top" class="mm_dialog_prompt_required">Default Search Radius:</td>
					<td valign="top" class="TGSL_Settings_dialog_field">
						<input type="text" name="TGSL_Settings:DefaultSearchRadius" id="TGSL_Settings:DefaultSearchRadius" value="{ encodeentities( l.tgsl_settings:DefaultSearchRadius ) }" />
					</td>
				</tr>
				<tr>
					<td valign="top" class="mm_dialog_prompt_required">Search Radius Options:</td>
					<td valign="top" class="TGSL_Settings_dialog_field">
						<textarea name="TGSL_Settings:SearchRadiusOptions" id="TGSL_Settings:SearchRadiusOptions">
							<MvEVAL EXPR = "{ encodeentities( l.tgsl_settings:SearchRadiusOptions ) }">
						</textarea>
					</td>
				</tr>
				<tr>
					<td valign="top" class="mm_dialog_prompt_required">Map Center Lat/ Lng:</td>
					<td valign="top" class="TGSL_Settings_dialog_field">
						<input name="TGSL_Settings:MapCenterLatLng" id="TGSL_Settings:MapCenterLatLng" value="{ encodeentities( l.tgsl_settings:MapCenterLatLng ) }" />
					</td>
				</tr>
				<tr>
					<td colspan="2" valign="top">
						<em>Example: 32.8242404, -117.389167</em>
					</td>
				</tr>
				<MvCOMMENT>
				<tr>
					<td valign="top" colspan="2"><hr class="tgsl_hr" /></td>
				</tr>
				</MvCOMMENT>
				<tr>
					<th colspan="2" align="left">Google Geocoding</th>
				</tr>
				<tr>
					<td valign="top" class="mm_dialog_prompt_required">API Key:</td>
					<td valign="top" class="TGSL_Settings_dialog_field">
						<input type="text" name="TGSL_Settings:Geocode_Apikey" id="TGSL_Settings:Geocode_Apikey" value="{ encodeentities( l.tgsl_settings:Geocode_Apikey ) }" />
					</td>
				</tr>
				<tr>
					<td valign="top" class="mm_dialog_prompt_required">API Url:</td>
					<td valign="top" class="TGSL_Settings_dialog_field">
						<input type="text" name="TGSL_Settings:Geocode_Url" id="TGSL_Settings:Geocode_Url" value="{ encodeentities( l.tgsl_settings:Geocode_Url ) }" />
					</td>
					<tr>
						<td colspan="2" valign="top">
							<em>Default: https://maps.googleapis.com/maps/api/geocode/json</em>
						</td>
					</tr>
				</tr>
				<MvCOMMENT>
				<tr>
					<td valign="top" colspan="2"><hr class="tgsl_hr" /></td>
				</tr>
				</MvCOMMENT>
				<tr>
					<th colspan="2" align="left">Google Maps</th>
				</tr>
				<tr>
					<td valign="top" class="mm_dialog_prompt_required">API Key:</td>
					<td valign="top" class="TGSL_Settings_dialog_field">
						<input type="text" name="TGSL_Settings:Maps_Apikey" id="TGSL_Settings:Maps_Apikey" value="{ encodeentities( l.tgsl_settings:Maps_Apikey ) }" />
					</td>
				</tr>
				<tr>
					<td valign="top" class="mm_dialog_prompt_required">API Url:</td>
					<td valign="top" class="TGSL_Settings_dialog_field">
						<input type="text" name="TGSL_Settings:Maps_Url" id="TGSL_Settings:Maps_Url" value="{ encodeentities( l.tgsl_settings:Maps_Url ) }" />
					</td>
				</tr>
				<tr>
					<td colspan="2" valign="top">
						<em>Default: https://maps.googleapis.com/maps/api/js</em>
					</td>
				</tr>
			</tbody>
		</table>
		<div class="mm_dialog_buttons_left">
			<input id="TGSL_Settings_dialog_button_cancel" type="button" value="Cancel">
		</div>
		<div class="mm_dialog_buttons_right"><input id="TGSL_Settings_dialog_button_save" type="button" value="Save"></div>
	</div>
	<!-- TGSL_Settings MM Dialog -->

	<div id="mm9_batchlistdialog_additionalfields_mmbatchlistdialog" class="mm9_dialog">
		<div class="mm9_dialog_container">
			<div id="mm9_batchlistdialog_additionalfields_mmbatchlistdialog_content_container" class="mm9_dialog_content_container">
				<div id="mm9_batchlistdialog_additionalfields_mmbatchlistdialog_close" class="mm9_dialog_close"><div></div></div>
				<div id="mm9_batchlistdialog_additionalfields_mmbatchlistdialog_title" class="mm9_dialog_title"></div>
				<div id="mm9_batchlistdialog_additionalfields_mmbatchlistdialog_content" class="mm9_dialog_content">
					<div id="mm9_batchlistdialog_additionalfields">
						<div id="js_batchlistdialog_additionalfields"></div>
					</div>
				</div>
				<div id="mm9_batchlistdialog_additionalfields_mmbatchlistdialog_actionbar" class="mm9_dialog_actionbar"></div>
			</div>
			<div id="mm9_batchlistdialog_additionalfields_mmbatchlistdialog_resize_top" class="mm9_dialog_resize_top"></div>
			<div id="mm9_batchlistdialog_additionalfields_mmbatchlistdialog_resize_right" class="mm9_dialog_resize_right"></div>
			<div id="mm9_batchlistdialog_additionalfields_mmbatchlistdialog_resize_bottom" class="mm9_dialog_resize_bottom"></div>
			<div id="mm9_batchlistdialog_additionalfields_mmbatchlistdialog_resize_left" class="mm9_dialog_resize_left"></div>
			<div id="mm9_batchlistdialog_additionalfields_mmbatchlistdialog_resize_topright" class="mm9_dialog_resize_topright"></div>
			<div id="mm9_batchlistdialog_additionalfields_mmbatchlistdialog_resize_bottomright" class="mm9_dialog_resize_bottomright"></div>
			<div id="mm9_batchlistdialog_additionalfields_mmbatchlistdialog_resize_bottomleft" class="mm9_dialog_resize_bottomleft"></div>
			<div id="mm9_batchlistdialog_additionalfields_mmbatchlistdialog_resize_topleft" class="mm9_dialog_resize_topleft"></div>
		</div>
	</div>
	<MvEVAL EXPR = "{ [ g.Module_Admin ].Element_Modal_HTML() }">

	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_System_Update" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "Module_System_Validate" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>


<MvCOMMENT>
| ====================================================================================
|	JSON
| ====================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "Module_JSON" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT [ g.Module_JSON ].JSON_Store_Open() }">
		<MvFUNCTIONRETURN>
	</MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'StoreLocator_Load_Query'		}"><MvFUNCTIONRETURN VALUE = "{ JSON_StoreLocator_Load_Query( l.module ) }"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Load_Additional_Fields'		}"><MvFUNCTIONRETURN VALUE = "{ JSON_Load_Additional_Fields( l.module ) }"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Location_Insert'				}"><MvFUNCTIONRETURN VALUE = "{ JSON_Location_Insert( l.module ) }"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Location_Update'				}"><MvFUNCTIONRETURN VALUE = "{ JSON_Location_Update( l.module ) }"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'Location_Delete'				}"><MvFUNCTIONRETURN VALUE = "{ JSON_Location_Delete( l.module ) }"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'AdditionalFields_Load_Query'	}"><MvFUNCTIONRETURN VALUE = "{ JSON_AdditionalFields_Load_Query( l.module ) }"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'AdditionalField_Insert'		}"><MvFUNCTIONRETURN VALUE = "{ JSON_AdditionalField_Insert( l.module ) }"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'AdditionalField_Update'		}"><MvFUNCTIONRETURN VALUE = "{ JSON_AdditionalField_Update( l.module ) }"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'AdditionalField_Delete'		}"><MvFUNCTIONRETURN VALUE = "{ JSON_AdditionalField_Delete( l.module ) }"></MvIF>
	<MvIF EXPR = "{ g.Module_Function EQ 'StoreLocator_Settings_Update'	}"><MvFUNCTIONRETURN VALUE = "{ JSON_Settings_Update( l.module ) }"></MvIF>
</MvFUNCTION>

<MvCOMMENT>
| ====================================================================================
|	CLIENTSIDE
| ====================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "Module_Clientside" PARAMETERS = "module var" STANDARDOUTPUTLEVEL = "text, compresswhitespace" ERROROUTPUTLEVEL = "">
	<MvIF EXPR = "{ g.Filename EQ 'StoreLocator.js' }">
		<MvEVAL EXPR = "{ [ g.Module_Clientside ].Module_Content_Type( l.module, 'text/javascript' ) }">
		<MvINCLUDE FILE = "js/functions.js" INTERPRET = "OFF">
		<MvINCLUDE FILE = "js/addl_functions.js" INTERPRET = "OFF">
		<MvINCLUDE FILE = "js/batchlist.js" INTERPRET = "OFF">
		<MvINCLUDE FILE = "js/addl_batchlist.js" INTERPRET = "OFF">
	</MvIF>
</MvFUNCTION>

<MvCOMMENT>
| ====================================================================================
|	COMPONENT
| ====================================================================================
</MvCOMMENT>

<MvFUNCTION NAME = "ComponentModule_Initialize" PARAMETERS = "module var, item, all_settings var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_End" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Tabs" PARAMETERS = "module var, item, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Validate" PARAMETERS = "module var, item, field_prefix, fields var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Update" PARAMETERS = "module var, item, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Content" PARAMETERS = "module var, item, tab, load_fields, field_prefix, fields var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Defaults" PARAMETERS = "module var, settings var" STANDARDOUTPUTLEVEL = "">
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Assign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Page_Unassign" PARAMETERS = "module var, page var, item, settings var" STANDARDOUTPUTLEVEL = "">
	<MvFUNCTIONRETURN VALUE = 1>
</MvFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Prerender" PARAMETERS = "module var, item, all_settings var, settings var, param" STANDARDOUTPUTLEVEL = "">
</MVFUNCTION>

<MvFUNCTION NAME = "ComponentModule_Render_Start" PARAMETERS = "module var, item, settings var, item_settings var, param" STANDARDOUTPUTLEVEL = "">
	<MvIF EXPR = "{ NOT Parse_Function_Parameters( l.param, l.function_name, l.parameters, l.parameter_count ) }">
		<MvFUNCTIONRETURN VALUE = 0>
	</MvIF>

	<MvCOMMENT>
	|
	|	Parameters & Function check
	|
	</MvCOMMENT>


	<MvIF EXPR = "{ ( l.function_name EQ 'load_settings' ) }">
		<MvASSIGN NAME = "l.new_param" VALUE = "{ 'Component_' $ l.function_name $ '( l.module, l.param, l.settings, ' $ l.parameters $ ' )' }">
	</MvIF>

	<MvCOMMENT>
	|
	|	tagerror
	|	22, 3 = display
	|	22, 0 = no display
	|
	|	FOR DEBUGGING, leave as 22, 3
	|
	</MvCOMMENT>

	<MvIF EXPR = "{ ISNULL l.new_param }">
		<MvFUNCTIONRETURN VALUE = "" />
	</MvIF>

	<MvASSEMBLY>
		.string asm_0 "l.new_param"
		.string asm_1 "g.Module_Root"
		.string asm_2 "l.module"
		.string asm_3 "module"
		.string asm_4 "g.MvDO_Error"

			pushc		asm_1
			pushn
			pushc		asm_2
			pushn
			pushc		asm_3
			memb_ro
			cat
			pushc		asm_0
			pushn
			tagerror	22, 0
			do_function
			pop
			tagerror	22, 0
			pushc		asm_4
			pushn
			jmp_eq		L_asm_success
			retn
		L_asm_success:
	</MvASSEMBLY>

</MvFUNCTION>

<MvINCLUDE FILE = "functions.mv">
<MvINCLUDE FILE = "component_helpers.mv">